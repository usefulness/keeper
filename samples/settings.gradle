pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal {
            content {
                excludeModule("io.github.usefulness", "keeper-gradle-plugin")
            }
        }
    }
    if (settings.hasProperty("agpVersion")) {
        def agpVersion = settings.agpVersion
        logger.quiet("AGP version=$agpVersion")
        resolutionStrategy.eachPlugin {
            if (requested.id.id.startsWith("com.android")) {
                useModule("com.android.tools.build:gradle:$agpVersion")
            }
        }
    }
}
plugins {
    id("org.gradle.toolchains.foojay-resolver-convention") version "1.0.0"
    id("com.gradle.develocity") version "4.3.2"
}

develocity {
    buildScan {
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"

        uploadInBackground = System.getenv("CI") == null
        publishing.onlyIf { false }
    }
}

dependencyResolutionManagement {
    repositories {
        google()
        mavenCentral()

        // Example demo of how to configure your own R8 repo
        repositories {
            // Limit this repo to only the R8 dependency
            exclusiveContent {
                forRepository {
                    maven {
                        url = "https://storage.googleapis.com/r8-releases/raw"
                        content { includeModule("com.android.tools", "r8") }
                    }
                }
                filter { includeModule("com.android.tools", "r8") }
            }
        }
    }
}

rootProject.name = "io.github.usefulness.keeper.sample"

include(":sample")
include(":sample-libraries:a")
include(":sample-libraries:b")
include(":sample-libraries:c")
include(":sample-libraries:test-only-android")
include(":sample-libraries:test-only-jvm")

includeBuild("..")