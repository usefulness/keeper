/*
 * Copyright (C) 2020. Slack Technologies, LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    `java-gradle-plugin`
    alias(libs.plugins.starter.library.kotlin)
    alias(libs.plugins.dokka)
    alias(libs.plugins.mavenPublish)
    alias(libs.plugins.binaryCompatibilityValidator)
    id("org.jetbrains.kotlin.plugin.sam.with.receiver") version libs.versions.kotlin.get()
}

samWithReceiver {
    annotation("org.gradle.api.HasImplicitReceiver")
}

val javaCompileVersion = JavaVersion.VERSION_25
val javaTargetVersion = JavaVersion.VERSION_17

tasks.withType<KotlinCompile>().configureEach {
    compilerOptions {
        apiVersion.set(KotlinVersion.KOTLIN_2_0)
        languageVersion.set(KotlinVersion.KOTLIN_2_0)
        freeCompilerArgs.add("-Xsam-conversions=class")
    }
}

tasks.withType<Test>().configureEach {
    beforeTest(closureOf<TestDescriptor> { logger.lifecycle("Running test: $this") })
    // Required to test configuration cache in tests when using withDebug()
    // https://github.com/gradle/gradle/issues/22765#issuecomment-1339427241
    jvmArgs(
        "--add-opens",
        "java.base/java.util=ALL-UNNAMED",
        "--add-opens",
        "java.base/java.util.concurrent.atomic=ALL-UNNAMED",
        "--add-opens",
        "java.base/java.lang.invoke=ALL-UNNAMED",
        "--add-opens",
        "java.base/java.net=ALL-UNNAMED",
    )
}

sourceSets {
    getByName("test").resources.srcDirs(project.layout.buildDirectory.dir("pluginUnderTestMetadata"))
}

kotlin {
    explicitApi()
    jvmToolchain(25)
}

gradlePlugin {
    plugins {
        plugins.create("keeper") {
            id = "com.slack.keeper"
            implementationClass = "com.slack.keeper.KeeperPlugin"
        }
    }
}


dokka {
    dokkaPublications.configureEach {
        suppressInheritedMembers.set(true)
        outputDirectory.set(rootDir.resolve("../docs/0.x"))
    }
    dokkaSourceSets.configureEach {
        skipDeprecated.set(true)
        externalDocumentationLinks.register("gradle-docs") {
            url("https://docs.gradle.org/${gradle.gradleVersion}/javadoc/allpackages-index.html")
        }
        externalDocumentationLinks.register("android-gralde-plugin-docs") {
            packageListUrl("https://developer.android.com/reference/tools/gradle-api/7.3/package-list")
            url("https://developer.android.com/reference/tools/gradle-api/7.3/classes")
        }
    }
}

mavenPublishing {
    publishToMavenCentral(automaticRelease = true)
    signAllPublications()
}

// Fix missing implicit task dependency in Gradle's test kit
tasks.named("processTestResources") { dependsOn("pluginUnderTestMetadata") }

val addTestPlugin: Configuration = configurations.create("addTestPlugin")

configurations { testImplementation.get().extendsFrom(addTestPlugin) }

tasks.pluginUnderTestMetadata {
    // make sure the test can access plugins for coordination.
    pluginClasspath.from(addTestPlugin)
}

dependencies {
    compileOnly(libs.kgp.api)
    compileOnly(libs.kgp)
    compileOnly(libs.zipflinger)
    compileOnly(libs.agp)

    addTestPlugin(libs.agp)
    addTestPlugin(libs.kgp)
    addTestPlugin(libs.kgp.api)
    testImplementation(libs.javapoet)
    testImplementation(libs.kotlinpoet)
    testImplementation(libs.truth)
    testImplementation(libs.junit)
}
